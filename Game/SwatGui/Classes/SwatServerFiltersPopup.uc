// ====================================================================
//  Class:  SwatGui.SwatServerFiltersPopup
//  Parent: SwatGuiPopup
//
//  Popup to select server filters.
// ====================================================================

class SwatServerFiltersPopup extends SwatGuiPopup
     ;

import enum EMPMode from Engine.Repo;

var(SWATGui) private EditInline Config GUICheckBoxButton	MyPingCheck;
var(SWATGui) private EditInline Config GUINumericEdit	    MyPingEdit;

var(SWATGui) private EditInline Config GUIComboBox	        MyGameTypeCombo;
var(SWATGui) private EditInline Config GUICheckBoxButton	MyGameTypeCheck;
var(SWATGui) private EditInline Config GUICheckBoxButton	MyPasswordedCheck;
var(SWATGui) private EditInline Config GUICheckBoxButton	MyFullCheck;
var(SWATGui) private EditInline Config GUICheckBoxButton	MyEmptyCheck;
var(SWATGui) private EditInline Config GUIComboBox	        MyMapNameCombo;
var(SWATGui) private EditInline Config GUICheckBoxButton	MyMapNameCheck;

var(SWATGui) private EditInline Config GUICheckBoxButton	MyHideIncompatibleVersionsCheck;
var(SWATGui) private EditInline Config GUICheckBoxButton	MyHideIncompatibleModsCheck;

function InitComponent(GUIComponent MyOwner)
{
    local int i;
    
	Super.InitComponent(MyOwner);

    MyPingCheck.OnChange=InternalOnChange;
    MyGameTypeCheck.OnChange=InternalOnChange;
	MyMapNameCheck.OnChange=InternalOnChange;

    for( i = 0; i < EMPMode.EnumCount; i++ )
    {
        MyGameTypeCombo.AddItem( GC.GetGameModeName(EMPMode(i)) );
    }

	LoadFullMapList();
}

function LoadFullMapList()
{
	local LevelSummary Summary;
	local string FileName;

    MyMapNameCombo.Clear();
    
    foreach FileMatchingPattern( "*.s4m", FileName )
    {
        //skip autoplay files (auto generated by UnrealEd)
        if( InStr( FileName, "autosave" ) != -1 )
            continue;
    
        //remove the extension
        if(Right(FileName, 4) ~= ".s4m")
			FileName = Left(FileName, Len(FileName) - 4);

        Summary = Controller.LoadLevelSummary(FileName$".LevelSummary");
        
        if( Summary == None )
        {
            log( "WARNING: Could not load a level summary for map '"$FileName$".s4m'" );
        }
        else
        {
			if (MyMapNameCombo.find(Summary.Title, true, true) == "")
				MyMapNameCombo.AddItem( Summary.Title );
        }
    }
}

function InternalOnActivate()
{
    MyPingEdit.SetValue( GC.theServerFilters.MaxPing, true );
    MyPingCheck.bForceUpdate = true;
    MyPingCheck.SetChecked( GC.theServerFilters.MaxPing >= 0 );

    MyGameTypeCombo.List.SetIndex( GC.theServerFilters.GameType );
    MyGameTypeCheck.bForceUpdate = true;
    MyGameTypeCheck.SetChecked( GC.theServerFilters.bFilterGametype );
	MyMapNameCheck.bForceUpdate = true;
    MyMapNameCheck.SetChecked( GC.theServerFilters.bFilterMapName );
    MyPasswordedCheck.SetChecked( GC.theServerFilters.bPassworded );
    MyFullCheck.SetChecked( GC.theServerFilters.bFull );
	MyEmptyCheck.SetChecked( GC.theServerFilters.bEmpty );
    
    MyHideIncompatibleVersionsCheck.SetChecked( GC.theServerFilters.bHideIncompatibleVersions );
    MyHideIncompatibleModsCheck.SetChecked( GC.theServerFilters.bHideIncompatibleMods );
    
#if IG_SWAT_MP_DEMO
    MyGameTypeCombo.DisableComponent();
    MyGameTypeCheck.DisableComponent();
#endif
}

protected function Confirm()
{
    if( MyPingCheck.bChecked )
        GC.theServerFilters.MaxPing = MyPingEdit.Value;
    else
        GC.theServerFilters.MaxPing = -1;

    GC.theServerFilters.GameType = EMPMode(MyGameTypeCombo.GetIndex());
	GC.theServerFilters.MapName = MyMapNameCombo.Get();

    GC.theServerFilters.bFilterGametype = MyGameTypeCheck.bChecked;
	GC.theServerFilters.bFilterMapName = MyMapNameCheck.bChecked;
    GC.theServerFilters.bPassworded = MyPasswordedCheck.bChecked;
    GC.theServerFilters.bFull = MyFullCheck.bChecked;
	GC.theServerFilters.bEmpty = MyEmptyCheck.bChecked;

    GC.theServerFilters.bHideIncompatibleVersions = MyHideIncompatibleVersionsCheck.bChecked;
    GC.theServerFilters.bHideIncompatibleMods = MyHideIncompatibleModsCheck.bChecked;

    GC.SaveConfig();
}

function InternalOnChange(GUIComponent Sender)
{
	switch (Sender)
	{
		case MyPingCheck:
		    MyPingEdit.SetEnabled(MyPingCheck.bChecked);
            break;
		case MyGameTypeCheck:
		    MyGameTypeCombo.SetEnabled(MyGameTypeCheck.bChecked);
            break;
		case MyMapNameCheck:
			MyMapNameCombo.SetEnabled(MyMapNameCheck.bChecked);
			break;
	}
}

defaultproperties
{
    OnActivate=InternalOnActivate
    Passback="Filters"
}
